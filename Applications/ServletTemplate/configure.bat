@ECHO OFF
SETLOCAL EnableDelayedExpansion
SET ROOT=%cd%
SET DIRECTORY=%~dp0
CALL :CreateForwardingScripts
CALL :ParseArgs %*
CALL :SetupDependencies
IF ERRORLEVEL 1 EXIT /B 1
CALL :CheckHashes
CALL :RunCMake
IF ERRORLEVEL 1 EXIT /B 1
CALL "!DIRECTORY!version.bat"
ENDLOCAL
EXIT /B !ERRORLEVEL!

:CreateForwardingScripts
IF NOT EXIST build.bat (
  >build.bat ECHO @ECHO OFF
  >>build.bat ECHO CALL "%~dp0build.bat" %%*
)
IF NOT EXIST configure.bat (
  >configure.bat ECHO @ECHO OFF
  >>configure.bat ECHO CALL "%~dp0configure.bat" %%*
)
EXIT /B 0

:ParseArgs
SET DEPENDENCIES=
SET IS_DEPENDENCY=
:ParseArgsLoop
SET ARG=%~1
IF "!IS_DEPENDENCY!"=="1" (
  SET DEPENDENCIES=!ARG!
  SET IS_DEPENDENCY=
  SHIFT
  GOTO ParseArgsLoop
) ELSE IF NOT "!ARG!"=="" (
  IF "!ARG:~0,3!"=="-DD" (
    SET IS_DEPENDENCY=1
  )
  SHIFT
  GOTO ParseArgsLoop
)
IF "!DEPENDENCIES!"=="" (
  SET DEPENDENCIES=!ROOT!\Dependencies
)
EXIT /B 0

:SetupDependencies
IF NOT EXIST "!DEPENDENCIES!" (
  MD "!DEPENDENCIES!"
)
PUSHD "!DEPENDENCIES!"
CALL "!DIRECTORY!..\..\Beam\setup.bat"
IF ERRORLEVEL 1 (
  ECHO Error: setup.bat failed.
  POPD
  EXIT /B 1
)
POPD
IF NOT "!DEPENDENCIES!"=="!ROOT!\Dependencies" (
  IF EXIST Dependencies (
    RD /S /Q Dependencies
  )
  mklink /j Dependencies "!DEPENDENCIES!" >NUL
)
EXIT /B 0

:CheckHashes
SET RUN_CMAKE=
IF NOT EXIST CMakeFiles (
  MD CMakeFiles
  SET RUN_CMAKE=1
)
SET TEMP_FILE=!ROOT!\temp_%RANDOM%%RANDOM%.txt
TYPE "!DIRECTORY!CMakeLists.txt" > "!TEMP_FILE!"
CALL :CheckFileHash "!TEMP_FILE!" "CMakeFiles\cmake_hash.txt"
IF EXIST "!DIRECTORY!Include" (
  DIR /a-d /b /s "!DIRECTORY!Include\*" > "!TEMP_FILE!"
  CALL :CheckFileHash "!TEMP_FILE!" "CMakeFiles\hpp_hash.txt"
)
IF EXIST "!DIRECTORY!Source" (
  DIR /a-d /b /s "!DIRECTORY!Source\*" > "!TEMP_FILE!"
  CALL :CheckFileHash "!TEMP_FILE!" "CMakeFiles\cpp_hash.txt"
)
EXIT /B 0

:CheckFileHash
SET CURRENT_HASH=
FOR /F "skip=1" %%H IN ('certutil -hashfile "%~1" SHA256') DO (
  IF NOT DEFINED CURRENT_HASH SET CURRENT_HASH=%%H
)
DEL "%~1"
IF EXIST "%~2" (
  SET /P CACHED_HASH=<"%~2"
  IF NOT "!CACHED_HASH!"=="!CURRENT_HASH!" SET RUN_CMAKE=1
) ELSE (
  SET RUN_CMAKE=1
)
IF "!RUN_CMAKE!"=="1" (
  >"%~2" ECHO !CURRENT_HASH!
)
SET CURRENT_HASH=
SET CACHED_HASH=
EXIT /B 0

:RunCMake
IF "!RUN_CMAKE!"=="1" (
  cmake -S "!DIRECTORY!" -DD="!DEPENDENCIES!"
  IF ERRORLEVEL 1 (
    ECHO Error: CMake configuration failed.
    EXIT /B 1
  )
)
EXIT /B 0
